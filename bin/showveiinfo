#!/usr/bin/perl

use warnings;
use strict;

use XML::Simple;
use LWP::Simple;

use Data::Dumper;

my $nominatimbase = "http://nominatim.openstreetmap.org/";
my $osmbase       = "http://www.openstreetmap.org/api/";

sub lookupNominatim {
    my ($latitude, $longitude) = @_;
    my $url =
    "${nominatimbase}reverse?format=xml&zoom=16&lat=$latitude&lon=$longitude";
    my $j = LWP::Simple::get($url);
    if ($j) {
        my $ref = XMLin($j);
        print STDERR "URL: $url\n";
        print STDERR Dumper($ref);
        return $ref;
    } else {
        print STDERR "No reply from $url\n";
    }
    return undef;
}
sub getOSMWayTags {
    my $wayid = shift;
    my $url = "${osmbase}0.6/way/$wayid";
    print STDERR "URL: $url\n";
    my $j = LWP::Simple::get($url);
    if ($j) {
        my $ref = XMLin($j);
        print STDERR Dumper($ref);
        my %tags;
        map { $tags{$_->{'k'}} = $_->{'v'} } @{$ref->{way}->{tag}};
        return \%tags;
    } else {
        print STDERR "No reply from $url\n";
    }
    return undef;
}

sub guessRoadOperator {
    my $inforef = shift;
    my $highway = $inforef->{highway} || "unknown";
    my $ref =  $inforef->{ref} || "unknown";

    my $operator;

    if ($highway eq "trunk"
        || $highway eq "primary"
        || $ref =~ m/E \d+/
        || $ref =~ m/Fv\d+/i
        ) {
        $operator = "Statens Vegvesen";
    }
    print STDERR "Guessing operator $operator\n" if defined $operator;
    return $operator;
}

sub getNearestRoadTags {
    my ($latitude, $longitude) = @_;
    my $inforef = lookupNominatim($latitude, $longitude);
    if ('way' eq $inforef->{result}->{osm_type}) {

        my $osmtags = getOSMWayTags($inforef->{result}->{osm_id});
        print STDERR Dumper $osmtags;
        unless (exists $osmtags->{operator}) {
            $osmtags->{operatorguess} = guessRoadOperator($osmtags);
            delete $osmtags->{operator};
        }
        return $osmtags;
    }
    return undef;
}

sub isRoadOperator {
    my ($latitude, $longitude, $operatorname) = @_;
    my $osmtags = getNearestRoadTags($latitude, $longitude);
    print STDERR Dumper($osmtags);
    my $operator = $osmtags->{operator} || $osmtags->{operatorguess};
    if ($operator) {
        my ($name, $ref) = ('','');
        $name = " named $osmtags->{name}" if exists $osmtags->{name};
        $ref = " ($osmtags->{ref})" if exists $osmtags->{ref};
        print STDERR "Claiming operator $operator for way$name$ref\n";
        return ($operator eq $operatorname);
    }
    return undef;
}

my $latitude = $ARGV[0] || 63.37638;
my $longitude = $ARGV[1] || 10.37595;

unless (isRoadOperator($latitude, $longitude, "Statens Vegvesen")) {
    print STDERR "Operator is not Statens Vegvesen\n";
}
